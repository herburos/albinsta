group 'com.albi'
version '1.0.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    sshAntTask
}

dependencies {
    compile 'com.sparkjava:spark-core:2.3'
    compile 'com.sparkjava:spark-template-thymeleaf:2.3'

    testCompile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile 'com.sachinhandiekar:jInstagram:1.1.5'

    testCompile group: 'junit', name: 'junit', version: '4.11'

    sshAntTask 'org.apache.ant:ant-jsch:1.9.6'
    sshAntTask 'com.jcraft:jsch:0.1.53'
}

ext.mainClassName = "Main"
jar {
    def manifestClasspath = configurations.runtime.filter {it.name.contains('.jar')}.collect { "$it.name" }.join(' ')

    manifest {
        attributes 'Implementation-Title' : 'Asset-Discovery',
                'Implementation-Version' : version,
                'Main-Class' : mainClassName,
                'Class-Path': manifestClasspath
    }
    destinationDir = file("$rootDir/distribution/")
}

task copyLibraries(type: Copy) {
    into "$rootDir/distribution/"
    from configurations.runtime.files
}

clean {
    delete "$rootDir/distribution"
}

task distribution(){
    dependsOn clean, jar, copyLibraries
}

ant.taskdef(
        name: 'scp',
        classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
        classpath: configurations.sshAntTask.asPath)


task uploadTo44(dependsOn: [distribution]) {
    doLast  {
        ant.scp(
                file: file(jar.archivePath),
                todir: 'root:drsweb@192.99.102.44:/data/bin/albinsta/distribution',
                trust: true)
    }
}